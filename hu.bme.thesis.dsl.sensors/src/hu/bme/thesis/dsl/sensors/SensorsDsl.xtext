grammar hu.bme.thesis.dsl.sensors.SensorsDsl with org.eclipse.xtext.common.Terminals

generate sensorsDsl "http://www.bme.hu/thesis/dsl/sensors/SensorsDsl"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	(
		mqttSetup = MqttSetup
		(messages += Message)*
		(sensors += Sensor)*
	)
;

MqttSetup:
	"mqttSetup" "{"
		"brokerUrl" brokerUrl = STRING
	"}" 
;

DataType:
	name = ("int" | "string" | "boolean" | "float")
;

Message:
	"message" "{"
		"name" name = ID
		(
			(dataParameters += DataParameter) |
			(messageParameters += MessageParameter)
		)*
		("qos" qos = INT)?
	"}"
;

terminal FLOAT:
	INT "." INT "f"
;

Value:
	{IntLiteral} value = INT |
	{StringLiteral} value = STRING |
	{BooleanLiteral} value = ("true" | "false") |
	{FloatLiteral} value = FLOAT
;

DataParameter:
	"parameter" type = DataType name = ID ("=" value = Value)? ("criteria" criteria += Criteria (criteria += Criteria)*)?
;

Criteria:
	critPrefix = ("min" | "max") critValue = CritValue
;

CritValue:
	{IntCriteria} value = INT |
	{FloatCriteria} value = FLOAT | 
	{BooleanCriteria} value = ("true" | "false") |
	{StringCriteria} value = STRING
;

MessageParameter:
	"parameter" message = [Message]
;

Sensor:
	"sensor" "{"
		"name" name = ID
		"messages" messages += [Message]
		("," messages += [Message])*
	"}"
;
