grammar hu.bme.thesis.dsl.SensorDsl with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes

generate sensorDsl "http://www.bme.hu/thesis/dsl/SensorDsl"

Model:
	(
		mqttSetup = MqttSetup
		(types += Type)*
		(sensors += Sensor)*
	)
;

MqttSetup:
	"mqttSetup" "{"
		"brokerUrl" brokerUrl = STRING
	"}" 
;

Type:
	DataType | MessageType
;

MessageType:
	"messageType" name=ID "{"
		(parameters += MessageParameter)*
		("qos" qos = INT)?
	"}"
;

DataType:
	"dataType" name = ID "mapped-to" type = [jvmTypes::JvmType|QualifiedName]
;

QualifiedName: ID ('.' ID)*;

MessageParameter:
	"parameter" type = [Type] name = ID
;

Sensor:
	"sensor" "{"
		"name" name = ID
		"messageType" messageType = [MessageType]
	"}"
;
